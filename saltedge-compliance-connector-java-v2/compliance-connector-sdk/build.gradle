plugins {
    id 'java'
    id 'maven'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'org.springframework.boot' version '2.2.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'com.bmuschko.nexus' version '2.3.1'
}

group 'com.saltedge.connector.sdk'
version '2.5.0'

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

repositories {
    mavenCentral()
    jcenter()
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}

dependencies {
    implementation 'org.jetbrains:annotations:16.0.2'
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    def jjwtVersion = '0.11.2'
    implementation "io.jsonwebtoken:jjwt-api:$jjwtVersion"
    runtime "io.jsonwebtoken:jjwt-impl:$jjwtVersion", "io.jsonwebtoken:jjwt-jackson:$jjwtVersion"
    implementation "io.jsonwebtoken:jjwt-impl:$jjwtVersion"
    implementation "io.jsonwebtoken:jjwt-jackson:$jjwtVersion"

    testImplementation "io.jsonwebtoken:jjwt-api:$jjwtVersion"
    testImplementation "io.jsonwebtoken:jjwt-jackson:$jjwtVersion"
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'com.h2database:h2:1.4.200'
}

// GENERATE SHADOW/FAT JAR
shadowJar {
    destinationDirectory = file('../out')
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
    from sourceSets.main.allSource
}

// UPLOAD SHADOW/FAT JAR TO LOCAL/REMOTE MAVEN REPO
uploadShadow {
    repositories {
        mavenDeployer {
            repository(url: uri("${rootProject.projectDir}/../../maven-repo"))
        }
    }
}

// UPLOAD JAR TO MAVEN CENTRAL REPO
archivesBaseName = project.name

modifyPom {
    project {
        name 'compliance-connector-sdk'
        description 'Salt Edge Compliance Connector Java SDK'
        url 'https://github.com/saltedge/compliance-examples/tree/master/saltedge-compliance-connector-java-v2/compliance-connector-sdk'
        inceptionYear '2020'

        scm {
            url 'https://github.com/saltedge/compliance-examples/tree/master/saltedge-compliance-connector-java-v2/compliance-connector-sdk'
            connection 'scm:https://github.com/saltedge/compliance-examples.git'
            developerConnection 'scm:https://github.com/saltedge/compliance-examples.git'
        }

        licenses {
            license {
                name 'The MIT License (MIT)'
                url 'https://raw.githubusercontent.com/saltedge/compliance-examples/master/saltedge-compliance-connector-java-v2/LICENSE.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'constantink'
                name 'Constantin Chelban'
                email 'constantink@saltedge.com'
            }
        }
    }
}

extraArchive {
    sources = true
    tests = true
    javadoc = true
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
}
